---
title: "Weighted Data"
author: "Andy Grogan-Kaylor"
date: "today"
format: 
  html:
    theme: yeti
    toc: true
    lightbox: true
    code-fold: true
    code-summary: "Show the code"
---

# Weights

In their simplest form, weights are the *inverse probability of selection*. If $p_i$ is the probability of selection, then the weight is defined as $w_i = \frac{1}{p_i}$.

# Simulate Population Data

```{r}
#| echo: false
#| output: false

library(Statamarkdown)

```

```{stata, collectcode = TRUE}
#| output: false

clear all

set seed 3846 // random seed

set obs 10000 // observations

generate x = rnormal(100, 10) // random normal x

generate z = rbinomial(1, .25) // dichotomous z

generate e = rnormal(0, 10) // random error

replace x = x - 20 if z == 1 // x is 20 lower for z=1

generate y = 2 * x + z + e // TRUE relationship in population

drop e // drop error

save population.dta, replace // save population data

quietly: regress y x i.z // population

est store population

```

```{stata}

use population.dta, clear

dtable x i.z y // descriptive statistics

```

# Random Sample

```{stata}

use population.dta, clear

sample 100, count by(z) // same count from each group

save sample.dta, replace // sample data

dtable x i.z y // descriptive statistics

```

# Generate Weights

```{stata}

* p is probability of selection
* w = 1/p

use sample.dta, clear

generate p = . // initialize to missing

replace p = 100/250 if z == 1

replace p = 100/750 if z == 0

generate w = 1/p

dtable x i.z y p w

save sample.dta, replace

```

# Descriptive Statistics

We see that in terms of *descriptive statistics*, the *weighted* estimates are much better than the *unweighted* estimates. 

```{stata}

use sample.dta, clear

svyset [pweight=w] // svyset the data

mean x // unweighted estimate

svy: mean x // weighted estimate 

```


# Regressions

Here the decision of whether *unweighted* or *weighted* results are better is not so clear.


```{stata}

use sample.dta, clear

quietly: regress y x i.z // unweighted

est store unweighted

quietly: regress y x i.z [pweight = w] // weighted

est store weighted

etable, ///
estimates(population unweighted weighted) ///
column(estimate) ///
showstars showstarsnote

```










